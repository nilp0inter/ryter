program ::= 'scriptio' identifier ';' block '.' 
identifier ::= letter (letter_or_digit)*
letter_or_digit ::= letter | digit
block ::= label_declaration_part constant_definition_part type_definition_part variable_declaration_part   procedure_and_function_declaration_part statement_part
label_declaration_part ::= empty | 'titulus' label (',' label)* ';'
label ::= unsigned_integer
constant_definition_part ::= empty | 'perennis' constant_definition ( ';' constant_definition)* ';' 
constant_definition ::= identifier '=' constant 
constant ::= unsigned_number | sign unsigned_number | constant_identifier | sign constant_identifier | string
unsigned_number ::= unsigned_integer | unsigned_real
unsigned_integer ::= digit (digit)*
unsigned_real ::= unsigned_integer '.' unsigned_integer | unsigned_integer '.' unsigned_integer 'E' scale_factor | unsigned_integer 'E' scale_factor
scale_factor ::= unsigned_integer | sign unsigned_integer
sign ::= '+' | '-'
constant_identifier ::= identifier
string ::= character (character)*
type_definition_part ::= empty | 'forma' type_definition (';'type_definition)*';'
type_definition ::= identifier '=' type
type ::= simple_type | structured_type | pointer_type
simple_type ::= scalar_type | subrange_type | type_identifier
scalar_type ::= (identifier (','identifier)*)
subrange_type ::= constant '..' constant
type_identifier ::= identifier
structured_type ::= array_type | record_type | set_type | file_type
array_type ::= 'dispositio' '[' index_type(','index_type)*']' 'de' component_type
index_type ::= simple_type
component_type ::= type
record_type ::= 'arca' field_list 'finis'
field_list ::= fixed_part | fixed_part ';' variant_part | variant_part
fixed_part ::= record_section (';'record_section)*
record_section ::= field_identifier (',' field_identifier )* ':' type | empty
variant_type ::= 'conditio' tag_field type_identifier 'de' variant ( ';' variant)*
tag_field ::= field_identifier ':' | empty
variant ::= case_label_list ':' ( field_list ) | empty
case_label_list ::= case_label (',' case_label)*
case_label ::= constant
set_type ::=set 'de' base_type
base_type ::= simple_type
file_type ::= 'papyrum' 'de' type
pointer_type ::= type_identifier
variable_declaration_part ::= empty | 'anima' variable_declaration (';' variable_declaration)* ';'
variable_declaration ::= identifier (','identifier)* ':' type
procedure_and_function_declaration_part ::= (procedure_or_function_declaration  ';')*
procedure_or_function_declaration  ::= procedure_declaration  | function_declaration 
procedure_declaration ::= procedure_heading block
procedure_heading ::= 'incantatio' identifier ';' | 'incantatio' identifier ( formal_parameter_section (';'formal_parameter_section)* )';'
formal_parameter_section ::= parameter_group | 'anima' parameter_group | 'incantamentum' parameter_group | 'incantatio' identifier ( ',' identifier)*
parameter_group ::= identifier (',' identifier)* ':' type_identifier
function_declaration ::= function_heading block
function_heading ::= 'incantamentum' identifier ':' result_type ';' | 'incantamentum' identifier ( formal_parameter_section (';'formal_parameter_section)* ) ':' result_type ';'
result_type ::= type_identifier
statement_part ::= compund_statement
statement ::= unlabelled_statement | label ':' unlabelled_statement
unlabelled_statement ::= simple_statement | structured_statement
simple_statement ::= assignment_statement | procedure_statement | go_to_statement | empty_statement
assignment_statement ::= variable ':=' expression | function_identifier ':=' expression
variable ::= entire_variable | component_variable | referenced_variable
entire_variable ::= variable_identifier
variable_identifier ::= identifier
component_variable ::= indexed_variable | field_designator | file_buffer
indexed_variable ::= array_variable '[' expression (',' expression)* ']'
array_variable ::= variable
field_designator ::= record_variable '.' field_identifier
record_variable ::= variable
field_identifier ::= identifier
file_buffer ::= file_variable
file_variable ::= variable
referenced_variable ::= pointer_variable
pointer_variable ::= variable
expression ::= simple_expression | simple_expression relational_operator simple_expression
relational_operator ::= '=' | '<>' | '<' | '<=' | '>=' | '>' | 'indu'
simple_expression ::= term | sign term| simple_expression adding_operator term
adding_operator ::= '+' | '-' | 'aut'
term ::= factor | term multiplying_operator factor
multiplying_operator ::= '*' | '/' | 'divisio' | 'reliquum' | 'atque'
factor ::= variable | unsigned_constant | ( expression ) | function_designator | set | 'non' factor
unsigned_constant ::= unsigned_number | string | constant_identifier nil
function_designator ::= function_identifier | function_identifier ( actual_parameter (',' actual_parameter)* )
function_identifier ::= identifier
set ::= '[' element_list ']'
element_list ::= element (',' element )* | empty
element ::= expression | expression '..' expression
procedure_statement ::= procedure_identifier | procedure_identifier (actual_parameter (',' actual_parameter )*)
procedure_identifier ::= identifier
actual_parameter ::= expression | variable | procedure_identifier | function_identifier
go_to_statement ::= 'apello' label
empty_statement ::= empty
empty ::=
structured_statement ::= compound_statement | conditional_statement | repetitive_statement | with_statement
compound_statement ::= 'initium' statement (';' statement )* 'finis' ';'
conditional_statement ::= if_statement | case_statement
if_statement ::= 'si' expression 'ergo' statement | 'si' expression 'ergo' statement 'aliter' statement
case_statement ::= 'conditio' expression 'de' case_list_element (';' case_list_element )* 'finis'
case_list_element ::= case_label_list ':' statement | empty
case_label_list ::= case_label (',' case_label )*
repetitive_statement ::= while_statement | repeat_statemant | for_statement
while_statement ::= 'dum' expression 'facere' statement
repeat_statement ::= 'repetitio' statement (';' statement)* 'donec' expression
for_statement ::= 'iterato' control_variable ':=' for_list 'facere' statement
control_variable ::= identifier
for_list ::= initial_value 'ad' final_value | initial_value 'usque ad' final_value
initial_value ::= expression
final_value ::= expression
with_statement ::= 'cum' record_variable_list 'facere' statement
record_variable_list ::= record_variable (',' record_variable)*
